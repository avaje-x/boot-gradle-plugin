group 'org.avaje.incubation'
version '0.1.1'

apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'signing'

repositories {
    mavenCentral()
}

dependencies {
    compile 'org.springframework.boot:spring-boot-loader-tools:1.4.1.RELEASE'
    compile gradleApi()
    compile localGroovy()
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from 'build/docs/javadoc'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }

            pom.project {
                name 'boot-gradle-plugin'
                packaging 'jar'
                description 'Gradle plugin for runnable jar/war using Spring boot loader'
                url 'http://github.com/ebean-orm/ebean-gradle-plugin'

                scm {
                    developerConnection 'scm:git:https://github.com/ebean-orm/ebean-gradle-plugin.git'
                }

                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id 'rbygrave'
                        name 'Robin Bygrave'
                    }
                }
            }
        }
    }
}